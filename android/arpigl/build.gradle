/*
 * Copyright (C) 2015  eBusiness Information
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */
apply plugin: 'com.android.model.library'

model {
    android {
        compileSdkVersion 24
        buildToolsVersion "23.0.2"

        defaultConfig {
            minSdkVersion.apiLevel 15
            targetSdkVersion.apiLevel 23
            versionCode 1
            versionName "0.3.4"
        }

        ndk {
            moduleName = "arpigl"
            ldLibs.addAll(['log', 'GLESv2', 'z'])
            cppFlags.addAll(['-std=c++11', '-fexceptions', '-frtti', '-Wall', '-Wno-comment', '-Wno-strict-aliasing'])
            cppFlags.addAll(['-I' + file("../../core/include"), '-I' + file("../../third"), '-I' + file("../../third/rapidjson"), '-I' + file("src/main/jni/ndk-modules")])
            stl = 'c++_shared'
        }
    }

    android.sources {
        main {
            jniLibs {
                source {
                    srcDir 'src/main/libs'
                }
            }
        }
    }

//    sourceSets.main {
//        jniLibs.srcDir 'src/main/lib'
//    }

//    android.buildTypes {
//        debug {
//            jniDebuggable true
//        }
//    }

}

// call regular ndk-build(.cmd) script from app directory
task ndkBuild(type: Exec) {
    commandLine 'ndk-build', '-j8', '-C', file('src/main').absolutePath
}

task assetZip(type: Zip) {
    description 'Zip the arpigl assets and copy to the assets directory.'
    archiveName = 'arpigl-assets.zip'
    from '../../assets'
    destinationDir file('src/main/assets')
}

build.dependsOn assetZip


tasks.all {
    task ->
        if (task.name.startsWith('compile') && task.name.contains('MainC')) {
            task.enabled = false
        }
        if (task.name.startsWith('link')) {
            task.enabled = false
        }
        if (task.name.endsWith('SharedLibrary') ) {
            task.dependsOn ndkBuild
        }
}


dependencies {
    compile fileTree(dir: 'lib', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:24.0.0'
}
